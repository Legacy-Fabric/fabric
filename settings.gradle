pluginManagement {
	repositories {
		gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		mavenLocal()
 		maven {
			name = 'Jitpack'
			url = 'https://jitpack.io'
		}
	}
	resolutionStrategy {
		eachPlugin {
			if (requested.id.id == "fabric-loom" && !requested.version?.endsWith("-SNAPSHOT")) {
				useModule("com.github.Chocohead.Fabric-Loom:fabric-loom:${requested.version}")
			}
		}
	}
}

rootProject.name = "rewoven-api"

def addModule(String dir, String name) {
	include(dir)
	project(":$dir").projectDir = new File("${rootDir.path}/modules/$dir")
	project(":$dir").name = name
}

def addModule(String dir, String subdir, String name) {
	include(dir)
	project(":$dir").projectDir = new File("${rootDir.path}/modules/$dir/$subdir")
	project(":$dir").name = name
}

addModule("base", "rewoven-api-base")
addModule("resources", "rewoven-resource-loader-v1")
addModule("client", "rendering", "rewoven-rendering-api-v1")
addModule("client", "keybindings", "rewoven-keybinding-api-v1")
addModule("networking", "rewoven-networking-api-v1")
addModule("crash-report-info", "rewoven-crash-report-info-v1")
addModule("lifecycle-events", "rewoven-lifecycle-events-v1")
addModule("entity-events", "rewoven-entity-events-v1")
addModule("server", "vanilla-commands", "rewoven-vanilla-command-api-v1")
addModule("server", "sponge-commands", "rewoven-sponge-command-api-v2")
addModule("server", "permissions", "rewoven-permissions-api-v1")
addModule("server", "gamerules", "rewoven-gamerule-api-v1")

include("testmods")
